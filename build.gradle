/*
 * Artcodes recognises a different marker scheme that allows the
 * creation of aesthetically pleasing, even beautiful, codes.
 * Copyright (C) 2013-2016  The University of Nottingham
 *
 *     This program is free software: you can redistribute it and/or modify
 *     it under the terms of the GNU Affero General Public License as published
 *     by the Free Software Foundation, either version 3 of the License, or
 *     (at your option) any later version.
 *
 *     This program is distributed in the hope that it will be useful,
 *     but WITHOUT ANY WARRANTY; without even the implied warranty of
 *     MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 *     GNU Affero General Public License for more details.
 *
 *     You should have received a copy of the GNU Affero General Public License
 *     along with this program.  If not, see <http://www.gnu.org/licenses/>.
 */
buildscript {
	repositories {
		jcenter()
	}
	dependencies {
		classpath 'com.android.tools.build:gradle:2.3.0-beta3'
	}
}

apply plugin: 'idea'
apply plugin: 'com.android.application'

repositories {
	jcenter()
	maven { url 'https://raw.github.com/vRallev/mvn-repo/master/' }
}

dependencies {
	compile project(':artcodes-scanner')
	compile 'com.android.support:appcompat-v7:25.1.0'
	compile 'com.android.support:design:25.1.0'
	compile 'com.android.support:cardview-v7:25.1.0'
	compile 'com.android.support:customtabs:25.1.0'
	compile 'com.android.support:palette-v7:25.1.0'
	compile 'com.android.support:recyclerview-v7:25.1.0'
	compile 'com.google.android.gms:play-services-base:10.0.1'
    compile 'com.google.android.gms:play-services-places:10.0.1'
	compile 'com.google.android.gms:play-services-analytics:10.0.1'
	compile 'com.google.android.gms:play-services-identity:10.0.1'
	compile 'com.google.android.gms:play-services-auth:10.0.1'
	compile 'fr.avianey.com.viewpagerindicator:library:2.4.1@aar'
	compile 'com.squareup.picasso:picasso:2.5.2'
	compile 'com.squareup.okhttp3:okhttp:3.5.0'
    testCompile 'junit:junit:4.12'
}

android {
	compileSdkVersion 25
	buildToolsVersion '25.0.2'

	dataBinding {
		enabled = true
	}

	signingConfigs {
		release {
			storeFile file(project.property('keystore'))
			storePassword project.property('keystore.password')
			keyAlias project.property('key.alias')
			keyPassword project.property('key.password')
		}
	}

	buildTypes {
		release {
			signingConfig signingConfigs.release
		}
		debug {
			signingConfig signingConfigs.release
		}
	}

	defaultConfig {
		applicationId "uk.ac.horizon.aestheticodes"
		versionName '3.3.3'
		versionCode 400060
		minSdkVersion 15
		targetSdkVersion 25
		signingConfig signingConfigs.release
	}

	lintOptions {
		abortOnError false
	}

    splits {
        abi {
            enable true
            reset()
            include 'x86', 'x86_64', 'armeabi', 'armeabi-v7a', 'mips', 'mips64', 'arm64-v8a'
            universalApk false
        }
    }

    // map for the version code
    project.ext.versionCodes = ['armeabi': 1, 'armeabi-v7a': 2, 'arm64-v8a': 3, mips: 4, mips64: 5, 'x86': 6, 'x86_64': 7 ]
    android.applicationVariants.all { variant ->
        // assign different version code for each output
        variant.outputs.each { output ->
            output.versionCodeOverride =
                    project.ext.versionCodes.get(output.getFilter(com.android.build.OutputFile.ABI), 0) + (defaultConfig.versionCode * 10)
        }
    }

	compileOptions {
		sourceCompatibility JavaVersion.VERSION_1_7
		targetCompatibility JavaVersion.VERSION_1_7
	}
}

task wrapper(type: Wrapper) {
	gradleVersion = '3.3'
}
